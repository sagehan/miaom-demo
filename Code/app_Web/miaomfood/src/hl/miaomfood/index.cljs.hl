(page "index.html"
      (:require
       [clojure.string  :as string]
       [datascript.core :as d :refer [q]]
       [javelin.core :refer [cell] :refer-macros [cell= destroy-cell!]]
       [miaomfood.rpc :as rpc]))

(add-initfn! (rpc/fetch-raw-db))

;; Cells ====================================================================
(defc folding-toggle true)
(defc adding-pool nil)

(defc db__cart (d/empty-db
                {:cuisine/id      {:db/unique      :db.unique/identity }
                 :cuisine/species {:db/cardinality :db.cardinality/many
                                   :db/valueType   :db.type/ref }}))

(def conn__cart (d/conn-from-db @db__cart))

;; state transition functions ===============================================
(def folding! #(swap! folding-toggle not))
(defn ordering! [cid] (reset! adding-pool [cid]))
(def unOrdering! #(reset! adding-pool nil))

;; Helper functions =================================================================
(def indexed (partial map-indexed vector))


;; Cell formulas ============================================================
(let [schema {:website/notices   {:db/cardinality :db.cardinality/many
                                  :db/valueType   :db.type/ref }
              :group/menus       {:db/cardinality :db.cardinality/many
                                  :db/valueType   :db.type/ref }
              :menu/categories   {:db/cardinality :db.cardinality/many
                                  :db/valueType   :db.type/ref }
              :category/cuisines {:db/cardinality :db.cardinality/many
                                  :db/valueType   :db.type/ref }
              :cuisine/id        {:db/unique      :db.unique/identity }
              :cuisine/species   {:db/cardinality :db.cardinality/many
                                  :db/isComponent true
                                  :db/valueType   :db.type/ref }}
      datoms  (cell= rpc/raw-db)
      db (cell= (d/db-with (d/empty-db schema) datoms))]

  (defc= site-title (d/q '[:find ?t . :where [_ :website/title ?t]] db))

  (defc= notices (d/q '[:find [?n ...]
                        :where
                        [_ :website/notices ?ns]
                        [?ns :notice/content ?n]]
                   db))

  (defc= restaurant-meta (d/q '[:find (pull ?e [:restaurant/name
                                                  :restaurant/phone
                                                  :restaurant/address
                                                  :restaurant/wechatID
                                                  :restaurant/rest-day
                                                  :restaurant/isClosed ]) .
                                  :where [?e :restaurant/name]]
                           db))

  (defc= menus (d/q '[:find [?mn ...]
                      :where [ _   :group/menus ?gm ]
                             [ ?gm :menu/name  ?mn ] ]
                 db))

  (defc= categories-of
    (fn  [menu]
      (d/q '[:find [?cn ...]
             :in $, ?mn
             :where
             [ ?e  :menu/name       ?mn ]
             [ ?e  :menu/categories ?mc ]
             [ ?mc :category/name   ?cn ] ]
        db, menu)))

  (defc= cuisines-of
    (fn [category]
      (d/q '[:find [?cid ...]
             :in $, ?cn
             :where
             [ ?e  :category/name     ?cn  ]
             [ ?e  :category/cuisines ?cc  ]
             [ ?cc :cuisine/id        ?cid ] ]
        db, category)))

  (defc= cuisine-details-of
    (fn [cuisine-id]
      (d/pull db ["*"] [:cuisine/id cuisine-id])))

  )


;; State query functions ====================================================
(defn check-selected-spec
  "Check the special cuisine's cart status"
  [ db cuisines-id ]
  (d/q '[:find ?sn ?sq
         :in $ ?cid
         :where [ ?e  :cuisine/id      ?cid ]
                [ ?e  :cuisine/species ?cs  ]
                [ ?cs :spec/name       ?sn  ]
                [ ?cs :spec/quantity   ?sq  ]]
       db, cuisines-id))


;; Helper elements =================================================================
(defelem show-or-hide
  [{:keys [state]} children]
  (map (fn [child] (child :slide-toggle state)) children))

(defelem equal-ratio-wrap [{:keys [ratio] :or {:ratio "golden"} :as attr} body]
  (let [attr (dissoc attr :ratio)]
    ((div attr) {:class (str "wrp_ratio--" ratio)}
      (div :class "wrp--stretched"
        body))))

(defelem tag [{:keys [leadingText trailingText] :as attr}]
  (let [ attr (dissoc attr :leadingText :trailingText)]
    ((div :class "tag"
       (b :class "tag__leading" :text leadingText)
       (b :class "tag__trailing" :text trailingText))
     attr)))

;; Component elements =================================================================
(defelem billboard [{:keys [greeting interval] :or {interval 3000} :as attr} ]
  (let [ note  (cell greeting)
         attr  (dissoc attr :interval :greeting) ]
    (with-let [elem (div attr (span :class "notice-board__notice" (cell= note )))]
      (with-init!
        (with-interval interval
          (when-let [hasValue (cell= (rand-nth notices))]
            (reset! note @hasValue)))))))

(defelem slideshow [{
                     :keys [ imgs interval default_img ]
                     :or   { interval 3000 default_img "img/logo.jpg" }
                     :as   attr
                     }]
  (let [ imgs   (cell= (->> (conj [] default_img (not-empty imgs))
                          (flatten)
                          (distinct)
                          (filterv #(not= nil %))))
         toggle (cell   [ [true] [false] [false] [false] ])
         attr   (dissoc attr :imgs :interval :default_img) ]
    (with-let [elem
               (equal-ratio-wrap
                attr
                (div (loop-tpl
                      :bindings [ [[t] im] (cell= (map vector toggle imgs)) ]
                      (img
                        :class  "slideshow__img wrp--centering"
                        :src    im
                        :toggle t)))
                (div :class "slideshow__pagination"
                  (loop-tpl
                   :bindings [[t] toggle]
                   (span
                     :class (cell= {:pagination__bullet true
                                    :pagination__bullet--active t})))))]
      (with-init!
        (with-interval interval
          (swap! toggle #(into (vector (last %)) (butlast %))))))))

(defelem navbar []
  (nav :class "nav"))

(defelem checkout-button [ {:keys [label btn__img] :or {label "提交订单"} :as attr} ]
  (a :class "btn-order"
    (span :class "btn-order__label" :text label)
    (img  :src btn__img)))

(defelem checkout-panel []
  (div :class "checkout-page"))

(defelem addition-button [{:keys [cid] :as attr} _]
  (let [attr (dissoc attr :cid)]
    (div attr
      :click #(ordering! cid))))

(defelem addition-tag [spec]
  (tag :class        "addition-tag"
       :leadingText   spec
       :trailingText ";TODO:"))

(defelem addition-panel [{:keys [cid] :as attr} _]
  (let [c        (cell= (cuisine-details-of cid))
        species  (cell= (:cuisine/species c))
        c_title  (cell= (:cuisine/name    c))
        s_names  (cell= (map (comp name :spec/name) species))
        attr     (dissoc attr :cid)]
    (div attr
      (div
        :class "addition-panel"
        (h2 :class "cuisine__title" c_title)
        (div :class "addition-panel__minus" "-")
        (div :class "addition-panel__add" "+")
        (div (loop-tpl
              :bindings [spec s_names]
              (addition-tag spec)))))))

(defelem counter-tag [cid]
  (tag :class        "counter-tag"
       :leadingText  "已选"
       :trailingText ";TODO: " ))

(defelem cuisine [{:keys [name depict species currency id] :or {currency "¥"} :as attr}]
  (let [price     (cell=  (string/join "/" (sort (map :spec/price species))))
        selected? (cell=  (or true (seq (check-selected-spec id db__cart))))
        imgsrc    (cell=  (str "img/" (first depict)))
        attr      (dissoc attr  :name :depict :species :currency)]
    (article attr
      (equal-ratio-wrap
       :ratio "4_3"
       :class "item__header"
       (img
         :class "cuisine__picture"
         :src   imgsrc))
      (div :class "item__body clearfix"
        (h2    :class "cuisine__title"    :text name)
        (small :class "cuisine__currency" :text currency)
        (span  :class "cuisine__price"    :text price)
        (addition-button :class "cuisine__button" :cid id)
        (cell= (cond selected? (counter-tag id  )))))))

(defelem v-Card
  "TODO:// Finally, and ideally, this page elements should have a
                   button which will give customer a vCard file after
                   itself is been clikced. Now, it just a card-style div"
  []
  (equal-ratio-wrap
   :ratio "golden"
   :class "card card--front"
   (ul
     (li :class "icon icon__phone"    :text (cell= (:restaurant/phone restaurant-meta)))
     (li :class "icon icon__location" :text (cell= (:restaurant/address restaurant-meta)))
     (li :class "icon icon__timer"    :text (cell= (:restaurant/rest-day restaurant-meta))))
   (img :src "img/motorbike.png")))

(defelem social-vCard
  ""
  []
  (equal-ratio-wrap
   :ratio "golden"
   :class "card card--back"
   (h1 :class "card__title icon icon__wechat" :text "关注微信")
   (figure
     (img        :class "card__img" :src "img/QR-1.jpg" :alt "喵姆餐厅微信服务号")
     (figcaption :class "clearfix"
       (h2 :text (cell= (:restaurant/name restaurant-meta)))
       (p  :text (cell= (str "id: " (:restaurant/wechatID restaurant-meta))))))

   (figure
     (img        :class "card__img" :src "img/QR-1.jpg" :alt "客服微信号")
     (figcaption :class "clearfix"
       (h2 :text "胖喵")
       (p  :text (cell= (str "id: " (:restaurant/phone restaurant-meta))))))))

;; Node-constructor functions ===============================================
(defn create-cuisine
  "Receive a cuisine id, generate a cuisine element"
  [cid]
  (let [c       (cell= (cuisine-details-of cid))
        c_title (cell= (:cuisine/name      c))
        c_img   (cell= (:cuisine/depict    c))
        c_spec  (cell= (:cuisine/species   c))  ]
    (cuisine :class   "item cuisine"
             :id      cid
             :name    c_title
             :depict  c_img
             :species c_spec )))

;; App skeleton =================================================================
(html
  (head
    (title :text site-title)
    (link
      :rel "shortcut icon"
      :href "img/motorbike.png")
    (link
      :rel "stylesheet"
      :href "stylesheets/main.css")
    (html-meta :charset "utf-8")
    (html-meta
      :http-equiv "Content-Type"
      :content "text/html; charset=utf-8")
    (html-meta
      :name "description"
      :content "美味披萨,焗饭,还有各种小吃,乌鲁木齐全城外送!")
    (html-meta
      :name "keywords"
      :content "喵姆餐厅 喵姆 Miaom 快餐 美味 乌鲁木齐快餐 披萨 焗饭 沙拉 烤翅")
    (html-meta
      :name "viewport"
      :content "initial-scale=1.0, width=device-width"))
  (body
    (noscript
      (div :id "noscript"
        (p "JavaScript is required to view this page.")))
    (button :click folding! "收起")
    (div :class "wrapper"
      (show-or-hide
       :state folding-toggle
       (slideshow
        :class "slideshow"
        :ratio "golden"
        :imgs  (cell ["img/Chicken.JPG" "img/热辣牛肉.JPG" "img/Salad.jpg"])
        :default_img "img/logo.jpg"))
      (div
        :class "banner clearfix"
        (<!-- "This wrapper div may finally been removed to Sagittarius!")
        (billboard
         :class "notice-board" :greeting "欢迎预订" :interval 3000)
        (checkout-button
         :label "提交订单" :btn__img "img/notepad.png"))
      (navbar)
      (checkout-panel)
      (div :class "menus"
        (loop-tpl
         :bindings [menu menus]
         (section
           :id    menu
           :class "section-wrapper clearfix"
           (loop-tpl
            :bindings [category (cell= (categories-of menu))]
            (section
              :id    category
              :class "section cuisine-category"
              (header
                :class "section__header"
                (h1 :class "section__title" :text category))
              (div
                :class "section__body item-list clearfix"
                (cell-map create-cuisine
                          (cell= (cuisines-of category)))))))))
      (footer :class "footer"
        (social-vCard)
        (v-Card))
      (cell= (cond (seq adding-pool)
                   (addition-panel
                    :class "modal"
                    :cid   (peek adding-pool)
                    :click unOrdering!)))
      (cell= (print adding-pool)))))
